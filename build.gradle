buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            url = 'http://repo.maven.apache.org/maven2'
        }
    }
    dependencies {
        classpath group: 'io.github.http-builder-ng', name: 'http-builder-ng-core', version: '1.0.3'
        classpath 'org.gretty:gretty:3.0.4'
    }
}


plugins {
    id 'java'
}

version 'unspecified'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}
apply plugin: 'maven'
apply plugin: 'war'
apply plugin: 'org.gretty'

gretty {
    servletContainer = 'tomcat9'
}

dependencies {
    compile 'org.eclipse.jetty.websocket:websocket-api:9.4.24.v20191120'
    compile 'org.eclipse.jetty.websocket:websocket-client:9.4.24.v20191120'
    compile 'mysql:mysql-connector-java:8.0.11'
    compile 'org.postgresql:postgresql:42.2.9'
    compile 'com.sun.mail:javax.mail:1.6.1'
    compile 'ca.uhn.hapi.fhir:hapi-fhir-base:4.2.0'
    compile 'ca.uhn.hapi.fhir:hapi-fhir-jpaserver-base:4.2.0'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.thymeleaf:thymeleaf:3.0.11.RELEASE'
    compile 'org.springframework:spring-web:5.2.3.RELEASE'
    compile 'org.springframework:spring-jdbc:5.2.3.RELEASE'
    compile 'org.apache.commons:commons-dbcp2:2.7.0'
    compile 'com.h2database:h2:1.4.200'
    compile 'org.webjars:bootstrap:3.3.7'
    compile 'org.webjars:Eonasdan-bootstrap-datetimepicker:4.17.43'
    compile 'org.webjars:font-awesome:5.8.2'
    compile 'org.webjars.bower:awesome-bootstrap-checkbox:1.0.1'
    compile 'org.webjars:jstimezonedetect:1.0.6'
    compile 'org.webjars:select2:4.0.3'
    compile 'org.webjars.bower:jquery:3.3.1'
    compile 'org.webjars.bower:moment:2.15.1'

    compile 'org.ebaysf.web:cors-filter:1.0.1'
    compile 'org.apache.derby:derby:10.14.2.0'
    compile 'org.apache.derby:derbynet:10.14.2.0'
    compile 'org.apache.derby:derbyclient:10.14.2.0'

    testCompile 'org.eclipse.jetty:jetty-servlets:9.4.24.v20191120'
    testCompile 'org.eclipse.jetty:jetty-servlet:9.4.24.v20191120'
    testCompile 'org.eclipse.jetty.websocket:websocket-server:9.4.24.v20191120'
    testCompile 'org.eclipse.jetty:jetty-server:9.4.24.v20191120'
    testCompile 'org.eclipse.jetty:jetty-util:9.4.24.v20191120'
    testCompile 'org.eclipse.jetty:jetty-webapp:9.4.24.v20191120'
    testCompile 'ca.uhn.hapi.fhir:hapi-fhir-test-utilities:4.2.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
    testCompile 'org.mockito:mockito-core:3.2.0'
    //compile 'ca.uhn.hapi.fhir:hapi-fhir-testpage-overlay:4.2.0:war'
    compile 'ca.uhn.hapi.fhir:hapi-fhir-testpage-overlay:4.2.0:classes'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'javax.interceptor:javax.interceptor-api:1.2.2'
}

task loadData{
    doLast {
        def dataDir = new File(projectDir, "fhirResourcesToLoad/")
        def http = groovyx.net.http.HttpBuilder.configure{
            request.uri = 'http://localhost:8080'
            request.contentType = 'application/json'
        }
        dataDir.listFiles().sort{ it.name }.each { file->
            if (file.name.startsWith(".")){
                return
            }
            println 'Trying to load '+file.name
            def resource = new groovy.json.JsonSlurper().parseText(file.text)
            http.put {
                request.uri.path = '/test-ehr/r4/'+resource.resourceType+'/'+resource.id
                request.body = file.text
                response.success {
                    println '... success. '
                }
            }
        }
    }
}

task loadBundleResource{
    doLast {
        def bundleFileDir = new File(projectDir, "bundlefile")
        def fileList = bundleFileDir.listFiles()
        println(fileList)
        fileList.each{bundleFile -> 
            println '------ Processing file ' + bundleFile.getName()
            def bundleResource = new groovy.json.JsonSlurper().parseText(bundleFile.text)
            println '---- bundleResource ' + bundleResource
            def http = groovyx.net.http.HttpBuilder.configure{
                request.uri = 'http://localhost:8080'
                request.contentType = 'application/json'
            }
        
            def entryResource = bundleResource.entry.resource
            entryResource.each { item->
                println 'Item type:' + item.resourceType + ' with id:' + item.id
                println 'Json of item: ' + groovy.json.JsonOutput.toJson(item)
                http.put {
                    request.uri.path = '/test-ehr/r4/'+item.resourceType+'/'+item.id
                    request.body = groovy.json.JsonOutput.toJson(item)
                    response.success {
                        println '... success. '
                    }
                }

            }
        }
    }
}

task loadRequestBuilder() {
    doLast {
        setup()
        cleanup()

    }
}

void setup() {
    exec {
        workingDir 'src/main/webapp/'
        commandLine 'git','clone','--single-branch', '--branch','settings-change-hotfix','https://github.com/HL7-DaVinci/crd-request-generator.git','tempFolder'
    }
    exec {
        workingDir 'src/main/webapp/tempFolder'
        commandLine 'npm', 'install'
    }
    exec {
        workingDir 'src/main/webapp/tempFolder'
        commandLine 'npm','run-script', 'build'
    }
}

void cleanup(){

        def folder = new File(rootProject.projectDir.absolutePath + "/src/main/webapp/WEB-INF/build")
        if(folder.exists()) {
            exec {
                workingDir 'src/main/webapp/'
                commandLine 'rm', '-rf', 'WEB-INF/build'
            }
        }

        exec {
            workingDir 'src/main/webapp'
            commandLine 'mv','tempFolder/build', 'WEB-INF/'
        }

        exec {
            workingDir 'src/main/webapp'
            commandLine 'rm','-rf','tempFolder'
        }

}
